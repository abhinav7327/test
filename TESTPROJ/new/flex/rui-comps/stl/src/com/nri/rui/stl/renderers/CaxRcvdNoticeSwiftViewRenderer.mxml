<?xml version="1.0" encoding="utf-8"?>

<!-- Item Renderer for CAX Receive Notice MT566 Swift View -->
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" 
	     width="100%" 
	     height="100%" 
	     horizontalAlign="center" 
	     verticalAlign="middle">
	     
    <mx:Script>
        <![CDATA[
        	import mx.events.CloseEvent;            
            import com.nri.rui.core.controls.XenosAlert;
            import com.nri.rui.core.containers.SummaryPopup;
            import com.nri.rui.core.Globals;
            
            import flash.events.MouseEvent;        
            import mx.core.UIComponent;
            import mx.managers.PopUpManager;
            
            private var sPopup : SummaryPopup;	

    		/**
        	 * Mouse Click event handler to open a popup with initialization and passing values.
        	 */ 
            public function handleMouseClick(event:MouseEvent):void {
        		
        		sPopup= SummaryPopup(PopUpManager.createPopUp(UIComponent(this.parentApplication), 
        		                                              SummaryPopup, 
        		                                              true));
        		
        		//Title
        		sPopup.title = this.parentApplication.xResourceManager.getKeyValue('stl.rawfile.title.mt566');
             	        		
                //set the width and height of the popup
                sPopup.width = 850;
        		sPopup.height = 500;    		
        		PopUpManager.centerPopUp(sPopup);        		
        		
        		sPopup.moduleUrl = Globals.CAX_RECEIVE_NOTICE_MT566_SWF+Globals.QS_SIGN+
        		                   Globals.GWY_REFERENCE_NO+Globals.EQUALS_SIGN+data.gwyReferenceNo+
        		                   Globals.AND_SIGN +Globals.MODE+Globals.EQUALS_SIGN + "MT566_VIEW";
        	}
        	
            /**
             * This method overrides the updateDisplayList method to hide the View icon 
             * of the DataGrid column's element if
             *  
             * 1) Not a Receive Notice record
             * 2) Data Source is not MT566RECON
             * 
             * otherwise show the view icon.
             */
             override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void { 
             	super.updateDisplayList(unscaledWidth, unscaledHeight);             	
             	if(data.typeOfRecord == "R" && data.dataSource == "MT566RECON") {
             		viewButton.includeInLayout = true;
             		viewButton.visible = true;
             	} else {
             		viewButton.includeInLayout = false;
             		viewButton.visible = false;
             	}
             }
        ]]>
    </mx:Script>
    
    <mx:Button id="viewButton" styleName="{'BtnView'}" width="18" height="18" label="{'View'}" click="handleMouseClick(event)"/>
    <mx:Label text="{data.dataSource}"/>
    
</mx:HBox>
